#!/usr/bin/env zsh

# Vim mode binds
bindkey -v
# Enable backspace
bindkey -M viins '^?' backward-delete-char
bindkey -M viins '^H' backward-delete-char
# Go to normal mode
bindkey -M viins 'jk' vi-cmd-mode
bindkey -M viins 'kj' vi-cmd-mode

# Set curosr to be beam in insert
function zle-keymap-select () {
  case $KEYMAP in
    vicmd) echo -ne '\e[2 q';;      # block no blink
    viins|main) echo -ne '\e[6 q';; # beam no blink
  esac
}
zle -N zle-keymap-select
zle-line-init() {
  zle -K viins # initiate `vi insert` as keymap (can be removed if `bindkey -V` has been set elsewhere)
  echo -ne "\e[6 q"
}
zle -N zle-line-init
echo -ne '\e[6 q' # Use beam shape cursor on startup.
preexec() { echo -ne '\e[6 q' ;} # Use beam shape cursor for each new prompt.

# Yank to the system clipboard
function vi-yank-xclip {
  zle vi-yank
  # Check if in WSL
  if uname -r | grep -iq "Microsoft" ; then
    clip() {
      powershell.exe -Command "Set-Clipboard -Value (\$input | Out-String)"
    }
  else
    clip() {
      xclip -i
    }
  fi

  echo "$CUTBUFFER" | clip
}

zle -N vi-yank-xclip
bindkey -M vicmd 'y' vi-yank-xclip
